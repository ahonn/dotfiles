{
  config,
  pkgs,
  lib,
  ...
}:
let
  dotfilesPath = "${config.home.homeDirectory}/.config/nix-darwin";
  secretsFile = "${config.home.homeDirectory}/.config/nix-secrets/env.zsh";

  # Define secrets: ENV_VAR_NAME -> 1Password path
  # To add a new secret, simply add a new entry here
  opSecrets = {
    CONTEXT7_API_KEY = "op://Personal/Context7/credential";
    EXA_API_KEY = "op://Personal/Exa/credential";
    # OPENAI_API_KEY = "op://Personal/OpenAI/api-key";
  };

  # Generate shell commands to fetch each secret
  secretFetchCommands = lib.concatStringsSep "\n" (
    lib.mapAttrsToList (envVar: opPath: ''
      _TMP_VAL=$(op read "${opPath}" 2>/dev/null || echo "")
      if [ -n "''$_TMP_VAL" ]; then
        echo "export ${envVar}=\"''$_TMP_VAL\"" >> "${secretsFile}"
        echo "  - ${envVar}: OK"
      else
        echo "  - ${envVar}: FAILED (check 1Password path: ${opPath})"
        FETCH_FAILED=1
      fi
    '') opSecrets
  );
in
{
  imports = [
    ./programs/zsh.nix
    ./programs/git.nix
    ./programs/tmux.nix
    ./programs/starship.nix
    ./programs/direnv.nix
    ./programs/claude-code.nix
  ];

  home.username = "yuexunjiang";
  home.homeDirectory = "/Users/yuexunjiang";
  home.stateVersion = "23.05";
  programs.home-manager.enable = true;

  home.sessionVariables = {
    EDITOR = "nvim";
  };

  fonts.fontconfig.enable = true;

  home.packages = with pkgs; [
    bat
    eza
    ripgrep
    difftastic
    devenv
    cz-cli
    codex
    rustup
    nodejs
    python3
    nodePackages.conventional-changelog-cli
    nerd-fonts.fira-code
    nerd-fonts.jetbrains-mono
  ];

  home.file = {
    ".czrc".source = config.lib.file.mkOutOfStoreSymlink "${dotfilesPath}/symlink/czrc.symlink";
    ".editorconfig".source = config.lib.file.mkOutOfStoreSymlink "${dotfilesPath}/symlink/editorconfig.symlink";
  };

  my.zsh.enable = true;
  my.git.enable = true;
  my.tmux.enable = true;
  my.starship.enable = true;
  my.direnv.enable = true;
  my.claude-code.enable = true;

  # Generate a helper script to fetch secrets from 1Password
  # Run manually: fetch-secrets
  home.file.".local/bin/fetch-secrets" = {
    executable = true;
    text = ''
      #!/bin/bash
      SECRETS_FILE="${secretsFile}"
      LOG_DIR="${config.home.homeDirectory}/.local/share"
      mkdir -p "$(dirname "$SECRETS_FILE")" "$LOG_DIR"

      export PATH="/opt/homebrew/bin:$PATH"

      if ! command -v op &> /dev/null; then
        echo "Error: 1Password CLI (op) not found"
        exit 1
      fi

      echo "Fetching secrets from 1Password..."
      FETCH_FAILED=0

      # Initialize secrets file
      cat > "$SECRETS_FILE" << 'HEADER'
# Auto-generated by fetch-secrets - DO NOT EDIT
# Managed secrets from 1Password

HEADER

      # Fetch each secret
      ${secretFetchCommands}

      chmod 600 "$SECRETS_FILE"

      if [ "$FETCH_FAILED" = "1" ]; then
        echo "Warning: Some secrets failed to fetch"
        exit 1
      else
        echo "All secrets written to $SECRETS_FILE"
      fi
    '';
  };

  # Fetch secrets during activation using launchctl asuser (has GUI/Touch ID access)
  home.activation.fetchSecrets = lib.hm.dag.entryAfter [ "writeBoundary" ] ''
    echo "Fetching secrets from 1Password..."
    # Run as user with GUI session context for Touch ID access
    /bin/launchctl asuser $(id -u ${config.home.username}) \
      ${config.home.homeDirectory}/.local/bin/fetch-secrets || \
      echo "Warning: Failed to fetch secrets. Run 'fetch-secrets' manually."
  '';
}
